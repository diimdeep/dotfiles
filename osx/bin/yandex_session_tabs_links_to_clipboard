#!/usr/bin/osascript

property _template : "[%name](<%url>)"

--search and replace function for template
on snr(tofind, toreplace, TheString)
	set ditd to text item delimiters
	set text item delimiters to tofind
	set textItems to text items of TheString
	set text item delimiters to toreplace
	if (class of TheString is string) then
		set res to textItems as string
	else -- if (class of TheString is Unicode text) then
		set res to textItems as Unicode text
	end if
	set text item delimiters to ditd
	return res
end snr

set prettyDate to do shell script "date '+%A, %B %d, %Y at %l:%M %p'"
set theTitle to "Bookmarks " & prettyDate
set urlList to ""

try

	tell application "Yandex"
		-- count the windows
		set winCount to count of windows
		-- loop through the windows
		repeat with i from 1 to winCount
			set tabCount to count of tabs in window i
			--set tabList to tabs of window i
			--repeat with aTab in tabList
			repeat with j from 1 to tabCount
				set aTab to tab j of window i
				set aLink to _template
				set aLink to my snr("%name", title of aTab, aLink)
				set aLink to my snr("%url", URL of aTab, aLink)
				set urlList to urlList & aLink & return
			end repeat
			set urlList to urlList & return
		end repeat

	end tell
	set urlList to urlList & ""

	-- tell application "Evernote"
	-- 	set theNote to create note with html urlList title theTitle notebook "Bookmarks"
	-- end tell

	set the clipboard to urlList
	display notification "Copied to clipboard." with title "Yandex session export"

on error the error_message number the error_number
	--display dialog "Error: " & the error_number & ". " & the error_message buttons {"OK"} default button 1
	set the clipboard to urlList
	display notification "There was error, but we did copy tabs to clipboard." with title "Session export"
end try
